apply plugin: 'java'
apply plugin: 'maven'

group = 'com.spaceshift'
version = '0.9.11'

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.buildDir = 'build-gradle'

//java.srcDirs = ['src']

ext.jmeVersion = "3.2_branch-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {

    // base
    compile 'com.github.JavaSaBr:RlibFX:4.1.2'

    // jME
    compile 'com.github.JavaSaBr:SimFX:1.0.3'
    compile "com.github.JavaSaBr.jmonkeyengine:jme3-core:$jmeVersion"
    compile "com.github.JavaSaBr.jmonkeyengine:jme3-effects:$jmeVersion"
    compile "com.github.JavaSaBr.jmonkeyengine:jme3-bullet:$jmeVersion"

    // extensions
    compile 'com.github.JavaSaBr:jme3-spaceshift-extension:1.1.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

defaultTasks 'clean', 'install'

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.version = version
        pom.artifactId = 'jme3-spaceshift-editor'
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}